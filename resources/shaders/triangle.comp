#version 460

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;
layout(binding = 0, rgba8) uniform writeonly image2D outImage;
layout(std430, binding = 1) readonly buffer DataBuffer
{
    uint words[];
} scene;

float LoadF(uint wordIndex)
{
    return uintBitsToFloat(scene.words[wordIndex]);
}

vec3 GradeColor(vec3 c)
{
    c *= 1.08;

    const vec3 lumaWeights = vec3(0.2126, 0.7152, 0.0722);
    float luma = dot(c, lumaWeights);
    c = mix(vec3(luma), c, 1.20);

    c = (c - vec3(0.5)) * 1.10 + vec3(0.5);
    c = clamp(c, vec3(0.0), vec3(8.0));
    c = (c * (2.51 * c + 0.03)) / (c * (2.43 * c + 0.59) + 0.14);
    return c;
}

void main()
{
    ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
    ivec2 imageSizePx = imageSize(outImage);
    if (pixel.x >= imageSizePx.x || pixel.y >= imageSizePx.y)
    {
        return;
    }

    float yaw = LoadF(3u);
    float pitch = LoadF(4u);
    float fovY = LoadF(6u);

    float cosPitch = cos(pitch);
    float sinPitch = sin(pitch);
    float cosYaw = cos(yaw);
    float sinYaw = sin(yaw);

    vec3 forward = normalize(vec3(cosPitch * cosYaw, sinPitch, cosPitch * sinYaw));
    vec3 worldUp = vec3(0.0, 1.0, 0.0);
    vec3 right = normalize(cross(forward, worldUp));
    vec3 up = normalize(cross(right, forward));

    vec2 uv = (vec2(pixel) + vec2(0.5)) / vec2(imageSizePx);
    vec2 ndc = uv * 2.0 - 1.0;
    ndc.y = -ndc.y;

    float aspect = float(imageSizePx.x) / float(imageSizePx.y);
    float tanHalfFov = tan(fovY * 0.5);
    vec3 rayDir = normalize(forward + right * (ndc.x * aspect * tanHalfFov) + up * (ndc.y * tanHalfFov));

    vec3 skyHorizon = vec3(0.92, 0.62, 0.34);
    vec3 skyTop = vec3(0.18, 0.35, 0.85);
    float skyT = clamp(rayDir.y * 0.5 + 0.5, 0.0, 1.0);
    vec3 sky = mix(skyHorizon, skyTop, skyT);

    vec3 sunDir = normalize(vec3(0.35, 0.78, 0.22));
    float sun = pow(max(dot(rayDir, sunDir), 0.0), 80.0);
    vec3 color = sky + vec3(1.0, 0.92, 0.75) * sun * 0.22;
    color = GradeColor(color);

    imageStore(outImage, pixel, vec4(color, 1.0));
}
